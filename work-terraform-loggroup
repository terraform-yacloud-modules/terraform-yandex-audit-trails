data "yandex_client_config" "client" {}

resource "yandex_iam_service_account" "audit_sa" {
  name        = "audit-service-account"
  description = "Service account for audit trails"
}

resource "yandex_logging_group" "logging_group" {
  name             = "a-trail"
  folder_id        = data.yandex_client_config.client.folder_id
}

resource "yandex_resourcemanager_folder_iam_member" "audit_sa_role" {
  folder_id = data.yandex_client_config.client.folder_id
  role      = "audit-trails.viewer"
  member    = "serviceAccount:${yandex_iam_service_account.audit_sa.id}"

  depends_on = [yandex_iam_service_account.audit_sa]
}

resource "yandex_resourcemanager_folder_iam_member" "audit_sa_logging_writer" {
  folder_id = data.yandex_client_config.client.folder_id
  role      = "logging.writer"
  member    = "serviceAccount:${yandex_iam_service_account.audit_sa.id}"

  depends_on = [yandex_iam_service_account.audit_sa]
}

resource "yandex_audit_trails_trail" "basic_trail" {
  name        = "a-trail"
  folder_id   = data.yandex_client_config.client.folder_id
  description = "Some trail description"

  labels = {
    key = "value"
  }

  service_account_id = yandex_iam_service_account.audit_sa.id

  logging_destination {
    log_group_id = yandex_logging_group.logging_group.id
  }

  filter {
    path_filter {
      any_filter {
        resource_id   = data.yandex_client_config.client.folder_id
        resource_type = "resource-manager.folder"
      }
    }
    event_filters {
      service = "storage"
      categories {
        plane = "DATA_PLANE"
        type  = "WRITE"
      }
      path_filter {
        any_filter {
          resource_id   = data.yandex_client_config.client.folder_id
          resource_type = "resource-manager.folder"
        }
      }
    }
  }

  depends_on = [
    yandex_resourcemanager_folder_iam_member.audit_sa_logging_writer,
    yandex_resourcemanager_folder_iam_member.audit_sa_role
  ]
}
